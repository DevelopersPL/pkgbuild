# Maintainer: Daniel Speichert <daniel@speichert.pl>
pkgname=godnsagent
pkgver=r31.d92cd60
pkgrel=1
pkgdesc='DNS Server'
url=https://github.com/DevelopersPL/godnsagent
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('go' 'git' 'mercurial')
backup=('etc/default/godnsagent')
install='godnsagent.install'

pkgver() {
  cd "$srcdir/$pkgname"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

source=(git+https://github.com/DevelopersPL/godnsagent.git)
md5sums=('SKIP')

build() {
    mkdir -p go/bin
    export GOPATH=`pwd`/go
    export GOBIN=`pwd`/go/bin
    cd $pkgname
    go get
    go build -ldflags "-X main.buildtime=`date -u '+%Y-%m-%d_%H:%M:%S'` -X main.buildver=${pkgver}"
}

package() {
  cd $pkgname
  
  install -D -m755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -D -m600 godnsagent.defaults "$pkgdir/etc/default/godnsagent"
  install -D -m644 godnsagent.service "$pkgdir/usr/lib/systemd/system/godnsagent.service"
}

# vim:set ts=2 sw=2 et:
