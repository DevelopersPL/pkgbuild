pkgbase=php56
_pkgbase=php
pkgname=('php56'
                 'php56-apache'
                 'php56-cgi'
                 'php56-fpm'
                 'php56-embed'
                 'php56-enchant'
                 'php56-gd'
                 'php56-intl'
                 'php56-ldap'
                 'php56-mcrypt'
                 'php56-odbc'
                 'php56-pear'
                 'php56-pgsql'
                 'php56-pspell'
                 'php56-snmp'
                 'php56-sqlite'
                 'php56-tidy'
                 'php56-xsl')

pkgver=5.6.17
pkgrel=1
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
makedepends=('apache' 'c-client' 'postgresql-libs' 'libldap' 'postfix'
             'sqlite' 'unixodbc' 'net-snmp' 'libzip' 'enchant' 'file' 'freetds'
             'libmcrypt' 'tidyhtml' 'aspell' 'libltdl' 'gd' 'icu'
             'curl' 'libxslt' 'openssl' 'db' 'gmp' 'systemd')

source=("http://us2.php.net/distributions/php-${pkgver}.tar.xz"
        'php.ini.patch' 'apache.conf'
        'php-fpm.service' 'php-fpm.tmpfiles')
md5sums=('5e080e4b7df5db24f1b64313f8114bd8'
         'fec9da94f58074204b42c1f7da43c496'
         '2825dd0b8d3ba61689f6b10ee6ad0318'
         'ac232d695bbcf7fcf0f2748e0ddb52d5'
         '12392414f768281ae96f292d0aa9669c')
validpgpkeys=('6E4F6AB321FDC07F2C332E3AC2BF0BC433CFC8B3')

# prefix for most paths
prefix=opt/php56

prepare() {
        cd ${srcdir}/${_pkgbase}-${pkgver}

        patch -p0 -i ${srcdir}/php.ini.patch
        # Just because our Apache 2.4 is configured with a threaded MPM by default does not mean we want to build a ZTS PHP.
        # Let's supress this behaviour and build a SAPI that works fine with the prefork MPM.
        sed '/APACHE_THREADED_MPM=/d' -i sapi/apache2handler/config.m4 -i configure
}

build() {
        local _phpconfig="--srcdir=../${_pkgbase}-${pkgver} \
                --config-cache \
                --prefix=/${prefix}/usr \
                --sbindir=/${prefix}/usr/bin \
                --sysconfdir=/${prefix}/etc/php \
                --localstatedir=/${prefix}/var \
                --with-layout=GNU \
                --with-config-file-path=/${prefix}/etc/php \
                --with-config-file-scan-dir=/${prefix}/etc/php/conf.d \
                --disable-rpath \
                --mandir=/${prefix}/usr/share/man \
                --with-kerberos \
                --with-libedit \
                "

        local _phpextensions="--enable-bcmath=shared \
                --enable-calendar=shared \
                --enable-dba=shared \
                --enable-exif=shared \
                --enable-ftp=shared \
                --enable-gd-native-ttf \
                --enable-intl=shared \
                --enable-mbstring \
                --enable-mysqlnd \
                --enable-opcache \
                --enable-phar=shared \
                --enable-posix=shared \
                --enable-shmop=shared \
                --enable-soap=shared \
                --enable-sockets=shared \
                --enable-sysvmsg=shared \
                --enable-sysvsem=shared \
                --enable-sysvshm=shared \
                --enable-zip=shared \
                --enable-phpdbg \
                --with-bz2=shared \
                --with-curl=shared \
                --with-db4=/usr \
                --with-enchant=shared,/usr \
                --with-fpm-systemd \
                --with-freetype-dir=/usr \
                --with-xpm-dir=/usr \
                --with-gd=shared,/usr \
                --with-gdbm \
                --with-gettext=shared \
                --with-gmp=shared \
                --with-iconv=shared \
                --with-icu-dir=/usr \
                --with-imap-ssl \
                --with-imap=shared \
                --with-jpeg-dir=/usr \
                --with-ldap=shared \
                --with-ldap-sasl \
                --with-libzip \
                --with-mcrypt=shared \
                --with-mhash \
                --with-mysql-sock=/run/mysqld/mysqld.sock \
                --with-mysqli=shared,mysqlnd \
                --with-openssl=shared \
                --with-pcre-regex=/usr \
                --with-pdo-mysql=shared,mysqlnd \
                --with-pdo-odbc=shared,unixODBC,/usr \
                --with-pdo-pgsql=shared \
                --with-pdo-sqlite=shared,/usr \
                --with-pgsql=shared \
                --with-png-dir=/usr \
                --with-pspell=shared \
                --with-snmp=shared \
                --with-sqlite3=shared,/usr \
                --with-tidy=shared \
                --with-unixODBC=shared,/usr \
                --with-xmlrpc=shared \
                --with-xsl=shared \
                --with-zlib \
                "

        EXTENSION_DIR=/${prefix}/usr/lib/php/modules
        export EXTENSION_DIR
        PEAR_INSTALLDIR=/${prefix}/usr/share/pear
        export PEAR_INSTALLDIR

        cd ${srcdir}/${_pkgbase}-${pkgver}

        # php
        mkdir -p ${srcdir}/build-php
        cd ${srcdir}/build-php
        ln -sf ../${_pkgbase}-${pkgver}/configure
        ./configure ${_phpconfig} \
                --without-pear \
                --disable-cgi \
                --enable-pcntl \
                ${_phpextensions}
        make

        # php-pear
        # reuse the previous run; this will save us a lot of time
        cp -a ${srcdir}/build-php ${srcdir}/build-pear
        cd ${srcdir}/build-pear
        ./configure ${_phpconfig} \
                --with-pear \
                ${_phpextensions}
        make

        # cgi and fcgi
        # reuse the previous run; this will save us a lot of time
        cp -a ${srcdir}/build-php ${srcdir}/build-cgi
        cd ${srcdir}/build-cgi
        ./configure ${_phpconfig} \
                --disable-cli \
                --enable-cgi \
                ${_phpextensions}
        make

        # apache
        cp -a ${srcdir}/build-php ${srcdir}/build-apache
        cd ${srcdir}/build-apache
        ./configure ${_phpconfig} \
                --disable-cli \
                --with-apxs2 \
                ${_phpextensions}
        make

        # fpm
        cp -a ${srcdir}/build-php ${srcdir}/build-fpm
        cd ${srcdir}/build-fpm
        ./configure ${_phpconfig} \
                --disable-cli \
                --enable-fpm \
                --with-fpm-user=http \
                --with-fpm-group=http \
                ${_phpextensions}
        make

        # embed
        cp -a ${srcdir}/build-php ${srcdir}/build-embed
        cd ${srcdir}/build-embed
        ./configure ${_phpconfig} \
                --disable-cli \
                --enable-embed=shared \
                ${_phpextensions}
        make
}

# check() {
#       cd ${srcdir}/build-php
#       export SNMP_TIMEOUT=1
#       export SNMP_RETRIES=0
#       export NO_INTERACTION=1
#       make test
#       echo
# }

package_php56() {
        pkgdesc='An HTML-embedded scripting language'
        depends=('pcre' 'libxml2' 'curl' 'libzip' 'c-client' 'libedit')
        backup=("${prefix}/etc/php/php.ini")
        provides=("php56=${pkgver}")
#       conflicts=('php')
        cd ${srcdir}/build-php
        make -j1 INSTALL_ROOT=${pkgdir} install
        install -d -m755 ${pkgdir}/${prefix}/usr/share/pear
        # install php.ini
        install -D -m644 ${srcdir}/${_pkgbase}-${pkgver}/php.ini-production ${pkgdir}/${prefix}/etc/php/php.ini
        install -d -m755 ${pkgdir}/${prefix}/etc/php/conf.d/

        # remove static modules
        rm -f ${pkgdir}${EXTENSION_DIR}/*.a
        # remove modules provided by sub packages
        rm -f ${pkgdir}${EXTENSION_DIR}/{enchant,gd,intl,ldap,mcrypt,odbc,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
        # remove empty directory
        rmdir ${pkgdir}/${prefix}/usr/include/php/include
        rmdir ${pkgdir}/${prefix}/var/{log,run}
        # fix broken link
#       ln -sf phar.phar ${pkgdir}/${prefix}/usr/bin/phar

        # Create symlink
        install -d ${pkgdir}/usr/bin
        ln -sf /${prefix}/usr/bin/php ${pkgdir}/usr/bin/php56
}

package_php56-cgi() {
        pkgdesc='CGI and FCGI SAPI for PHP'
        depends=("php56=${pkgver}")
        provides=("php56-cgi=${pkgver}")
#       conflicts=('php-cgi')

        install -D -m755 ${srcdir}/build-cgi/sapi/cgi/php-cgi ${pkgdir}/${prefix}/usr/bin/php-cgi
}

package_php56-apache() {
        pkgdesc='Apache SAPI for PHP'
        depends=("php56=${pkgver}" 'apache')
        backup=('etc/httpd/conf/extra/php56_module.conf')
        provides=("php56-apache=${pkgver}")
#       conflicts=('php-apache')

        install -D -m755 ${srcdir}/build-apache/libs/libphp5.so ${pkgdir}/usr/lib/httpd/modules/libphp56.so
        install -D -m644 ${srcdir}/apache.conf ${pkgdir}/etc/httpd/conf/extra/php56_module.conf
}

package_php56-fpm() {
        pkgdesc='FastCGI Process Manager for PHP'
        depends=("php56=${pkgver}" 'systemd')
        backup=("${prefix}/etc/php/php-fpm.conf")
        install='php-fpm.install'
        provides=("php56-fpm=${pkgver}")
#       conflicts=('php-fpm')

        install -D -m755 ${srcdir}/build-fpm/sapi/fpm/php-fpm ${pkgdir}/${prefix}/usr/bin/php-fpm
        install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.8 ${pkgdir}/${prefix}/usr/share/man/man8/php-fpm.8
        install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.conf ${pkgdir}/${prefix}/etc/php/php-fpm.conf
        install -d -m755 ${pkgdir}/${prefix}/etc/php/php-fpm.d
#        install -D -m644 ${srcdir}/build-fpm/sapi/fpm/www.conf ${pkgdir}/${prefix}/etc/php/php-fpm.d/www.conf
        sed -i "s/listen =.*/listen = \/run\/php56-fpm\/php-fpm.sock/" ${pkgdir}/${prefix}/etc/php/php-fpm.conf
        install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/php56-fpm.conf
        install -D -m644 ${srcdir}/php-fpm.service ${pkgdir}/usr/lib/systemd/system/php56-fpm.service
}

package_php56-embed() {
        pkgdesc='Embedded PHP SAPI library'
        depends=("php56=${pkgver}")
        provides=("php56-embed=${pkgver}")
#       conflicts=('php-embed')

        install -D -m755 ${srcdir}/build-embed/libs/libphp5.so ${pkgdir}/usr/lib/libphp56.so
        install -D -m644 ${srcdir}/${_pkgbase}-${pkgver}/sapi/embed/php_embed.h ${pkgdir}/${prefix}/usr/include/php/sapi/embed/php_embed.h
}

package_php56-pear() {
    pkgdesc='PHP Extension and Application Repository'
    depends=("php56=${pkgver}")
#    conflicts=('php-pear')
    backup=("${prefix}/etc/php/pear.conf")

    cd ${srcdir}/build-pear
    make install-pear INSTALL_ROOT=${pkgdir}
    rm -rf ${pkgdir}/${prefix}/usr/share/pear/.{channels,depdb,depdblock,filemap,lock,registry}
}

package_php56-enchant() {
        pkgdesc='enchant module for PHP'
        depends=("php56=${pkgver}" 'enchant')
        provides=("php56-enchant=${pkgver}")
#       conflicts=('php-enchant')

        install -D -m755 ${srcdir}/build-php/modules/enchant.so ${pkgdir}/${prefix}/usr/lib/php/modules/enchant.so
}

package_php56-gd() {
        pkgdesc='gd module for PHP'
        depends=("php56=${pkgver}" 'gd')
        provides=("php56-gd=${pkgver}")
#       conflicts=('php-gd')

        install -D -m755 ${srcdir}/build-php/modules/gd.so ${pkgdir}/${prefix}/usr/lib/php/modules/gd.so
}

package_php56-intl() {
        pkgdesc='intl module for PHP'
        depends=("php56=${pkgver}" 'icu')
        provides=("php56-intl=${pkgver}")
#       conflicts=('php-intl')

        install -D -m755 ${srcdir}/build-php/modules/intl.so ${pkgdir}/${prefix}/usr/lib/php/modules/intl.so
}

package_php56-ldap() {
        pkgdesc='ldap module for PHP'
        depends=("php56=${pkgver}" 'libldap')
        provides=("php56-ldap=${pkgver}")
#       conflicts=('php-ldap')

        install -D -m755 ${srcdir}/build-php/modules/ldap.so ${pkgdir}/${prefix}/usr/lib/php/modules/ldap.so
}

package_php56-mcrypt() {
        pkgdesc='mcrypt module for PHP'
        depends=("php56=${pkgver}" 'libmcrypt' 'libltdl')
        provides=("php56-mcrypt=${pkgver}")
#       conflicts=('php-mcrypt')

        install -D -m755 ${srcdir}/build-php/modules/mcrypt.so ${pkgdir}/${prefix}/usr/lib/php/modules/mcrypt.so
}


package_php56-odbc() {
        pkgdesc='ODBC modules for PHP'
        depends=("php56=${pkgver}" 'unixodbc')
        provides=("php56-odbc=${pkgver}")
#       conflicts=('php-odbc')

        install -D -m755 ${srcdir}/build-php/modules/odbc.so ${pkgdir}/${prefix}/usr/lib/php/modules/odbc.so
        install -D -m755 ${srcdir}/build-php/modules/pdo_odbc.so ${pkgdir}/${prefix}/usr/lib/php/modules/pdo_odbc.so
}

package_php56-pgsql() {
        pkgdesc='PostgreSQL modules for PHP'
        depends=("php56=${pkgver}" 'postgresql-libs')
        provides=("php56-pgsql=${pkgver}")
#       conflicts=('php-pgsql')

        install -D -m755 ${srcdir}/build-php/modules/pgsql.so ${pkgdir}/${prefix}/usr/lib/php/modules/pgsql.so
        install -D -m755 ${srcdir}/build-php/modules/pdo_pgsql.so ${pkgdir}/${prefix}/usr/lib/php/modules/pdo_pgsql.so
}

package_php56-pspell() {
        pkgdesc='pspell module for PHP'
        depends=("php56=${pkgver}" 'aspell')
        provides=("php56-pspell=${pkgver}")
#       conflicts=('php-pspell')

        install -D -m755 ${srcdir}/build-php/modules/pspell.so ${pkgdir}/${prefix}/usr/lib/php/modules/pspell.so
}

package_php56-snmp() {
        pkgdesc='snmp module for PHP'
        depends=("php56=${pkgver}" 'net-snmp')
        provides=("php56-snmp=${pkgver}")
#       conflicts=('php-snmp')

        install -D -m755 ${srcdir}/build-php/modules/snmp.so ${pkgdir}/${prefix}/usr/lib/php/modules/snmp.so
}

package_php56-sqlite() {
        pkgdesc='sqlite module for PHP'
        depends=("php56=${pkgver}" 'sqlite')
        provides=("php56-sqlite=${pkgver}")
#       conflicts=('php-sqlite')

        install -D -m755 ${srcdir}/build-php/modules/sqlite3.so ${pkgdir}/${prefix}/usr/lib/php/modules/sqlite3.so
        install -D -m755 ${srcdir}/build-php/modules/pdo_sqlite.so ${pkgdir}/${prefix}/usr/lib/php/modules/pdo_sqlite.so
}

package_php56-tidy() {
        pkgdesc='tidy module for PHP'
        depends=("php56=${pkgver}" 'tidyhtml')
        provides=("php56-tidy=${pkgver}")
#       conflicts=('php-tidy')

        install -D -m755 ${srcdir}/build-php/modules/tidy.so ${pkgdir}/${prefix}/usr/lib/php/modules/tidy.so
}

package_php56-xsl() {
        pkgdesc='xsl module for PHP'
        depends=("php56=${pkgver}" 'libxslt')
        provides=("php56-xsl=${pkgver}")
#       conflicts=('php-xsl')

        install -D -m755 ${srcdir}/build-php/modules/xsl.so ${pkgdir}/${prefix}/usr/lib/php/modules/xsl.so
}
